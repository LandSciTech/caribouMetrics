model{

#Observation models
for(k in 1:nSurvs){
	_survString_
}
for(k in 1:nCounts){
	calves[count_id[k]] ~ dbin( R[count_id[k]], CountAntlerless[count_id[k]] )
}

#Survival
for(k in 1:nYears){
	#Adult female survival - process variance
	epsilon.Saf[k]~dnorm(0,tau.Saf)T(-15,0.3) #NOTE: by definition, exp(epsilon) can't be bigger than 1
	log(Saf2[k]) <- l.Saf + anthro[k]*beta.Saf + epsilon.Saf[k]
	#NOTE: by definition S.annual.KM can't be bigger than 1. Note adjustment as in Johnson et al 2020.
	S.annual.KM[k] <- min((Saf2[k]*46-0.5)/45,1)
}

#Recruitment
for(k in 1:nYears){
		epsilon.R[k]~dnorm(0,tau.R)T(-15,1) #NOTE: by definition, exp(epsilon) can't be bigger than 1
		log(R[k]) <- l.R + anthro[k]*beta.Rec.anthro + fire[k]*beta.Rec.fire + epsilon.R[k]
}

#Growth
for(k in 1:nYears){
		_adjustString_
		survivors[k] ~ dbin( S.annual.KM[k], fpop.size[k] )
    recruits[k] ~ dpois(Rfemale[k]*survivors[k])
    #small minimum in demoninator to give 0 when fpop.size=0
    pop.growthr[k] <- (survivors[k]+recruits[k])/max(fpop.size[k],0.0000001)
}
for(k in 2:assessmentYrs){
    pop.growth[k-1] <- pop.growthr[k-1]
}
for(k in assessmentYrs:nYears){
    pop.growth[k] <- mean(pop.growthr[(k-assessmentYrs+1):k])
}

fpop.size[1] <- Ninit
for(k in 2:nYears){
   fpop.size[k] <- survivors[k-1]+recruits[k-1]
}

# priors
	beta.Saf~dnorm(beta.Saf.Prior1,pow(beta.Saf.Prior2, -2))
	beta.Rec.anthro~dnorm(beta.Rec.anthro.Prior1,pow(beta.Rec.anthro.Prior2, -2))
	beta.Rec.fire~dnorm(beta.Rec.fire.Prior1,pow( beta.Rec.fire.Prior2, -2))
	composition.bias = bias.Prior1+0*bias.Prior2#~dnorm(bias.Prior1,pow(bias.Prior2,-2))

	#NOTE truncate distribution to ensure intercept between 0 and 0.99. See thinkingAboutPriors.R
	l.Saf ~ dnorm(l.Saf.Prior1,pow(l.Saf.Prior2,-2)) T(-10,-0.01)
	#NOTE: see addInterannualVar fn in caribouMetrics for logic. But note shape of interannual variation distribution is different because using normally distributed random effect.
	sig.Saf~dnorm(exp(l.Saf.Prior1)*sig.Saf.Prior1,pow(sig.Saf.Prior2,-2))
	tau.Saf<-pow(sig.Saf, -2)
	var.Saf<-1/tau.Saf
	var.Saf.real<-(var.Saf * meanAFsurv^2 * (1-meanAFsurv)^2)

	l.R ~ dnorm(l.R.Prior1,pow(l.R.Prior2,-2))
	sig.R~dnorm(exp(l.R.Prior1)*sig.R.Prior1,pow(sig.R.Prior2,-2))
	tau.R<-pow(sig.R, -2)
	var.R<-1/tau.R
	var.R.real<-(var.R * meanR^2 * (1-meanR)^2)

	meanAFsurv <-  exp(l.Saf)/(1+ exp(l.Saf)) #(prod(R))^(1/(nYears))
	meanR <-  exp(l.R)/(1+ exp(l.R)) #(prod(R))^(1/(nYears))
	meanRfemale <-  (prod(Rfemale))^(1/(nYears))
	medianLambda <- (prod(pop.growthr))^(1/(nYears))
	meanLambda <- mean(pop.growthr)
}
